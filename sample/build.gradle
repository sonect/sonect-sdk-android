apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'

def getVersionCode = { ->
    def DEFAULT_CODE = 1
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : DEFAULT_CODE
    println "VersionCode is set to $code"
    return code
}

def sonectSdkVersion = "4.17.1"

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "ch.sonect.sdk.sample"
        minSdk 21
        targetSdk 30
        versionCode getVersionCode()
        versionName "1.0+" + getVersionCode()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        missingDimensionStrategy 'sdkDependency', 'external'
        missingDimensionStrategy 'outerInner', 'internal'
        missingDimensionStrategy 'okhttpVersion', 'v4'
        missingDimensionStrategy 'minSdkVersion', 'minSdk21'
    }

    signingConfigs {
        // Custom debug store to be sure that all tools e.g. have the same
        // hash of the key to work properly.
        customDebug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.customDebug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.customDebug
        }
    }

    flavorDimensions("sdkDependency")
    productFlavors {
        internal {
            dimension = "sdkDependency"
            firebaseAppDistribution {
                groups="internal"
                releaseNotes="Internal RC Sonect SDK version inside"
            }
        }
        external {
            dimension = "sdkDependency"
            firebaseAppDistribution {
                groups="internal"
                releaseNotes="Sonect SDK version inside: $sonectSdkVersion"
            }
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file('../lint.xml')
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.1.3'
    }
}

dependencies {
    // For Sonect internal usage and direct dependency to source code
    internalImplementation project(path: ':sdk-user')
    internalImplementation project(path: ':sdk-common')
    internalImplementation project(path: ':sdk-data')
    internalImplementation project(path: ':sdk-domain')
    internalImplementation project(path: ':okhttp')

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    externalImplementation("com.github.sonect:android-user-sdk:$sonectSdkVersion") {
        exclude module: "okhttp4"
    }

    externalImplementation "com.squareup.okhttp3:okhttp:3.14.9"
    externalImplementation "com.squareup.okhttp3:logging-interceptor:3.14.9"
}

apply plugin: 'com.google.gms.google-services'