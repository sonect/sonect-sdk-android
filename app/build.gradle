apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'

def getVersionCode = { ->
    def DEFAULT_CODE = 1
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : DEFAULT_CODE
    println "VersionCode is set to $code"
    return code
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ch.sonect.sdk.sample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    signingConfigs {
        // Custom debug store to be sure that all tools e.g. have the same
        // hash of the key to work properly.
        customDebug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.customDebug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                groups="internal"
            }
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.customDebug
        }
    }
}

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:1.3.1'
    }
}

dependencies {
    implementation('com.github.sonect:android-user-sdk:1.2.10@aar') {
        transitive = true
        exclude group: "idenfySdk"
        exclude group: "io.anyline"
    }
}
